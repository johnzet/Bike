*****************************************************************
******STMicroelectronics MOSFET, IGBT and Bipolar Library *******
*****************************************************************
*								            				    *
* Models provided by STMicroelectronics are not guaranteed to   *
* fully represent all the specifications and operating	        *
* characteristics of the product behavior that they reproduce.  *
* The model describes the characteristics of a typical device.  *
* In all cases, the current product data sheet contains all     *
* information to be used like final design guidelines and the   *
* only actual performance specification.                        *
* Altough models can be a useful tool in evaluating device      *
* performance, they cannot model exact device performance under *
* all conditions.						                        *
* STMicroelectronics therefore does not assume any              *
* responsibility arising from their use.                        *
* STMicroelectronics reserves the right to change models        *
* without prior notice.                                         *
*                                                               *
* Note: The model doesn't take into account the drain, gate,    *
* source inductances.If these contributions have to be          *
* considered it is possible include the inductors externally.   *
* For this package the values can be estimated as follow        *
*                                                               *
* Ldrain= 1nH ,Lsource=2nH and Lgate=2.5nH                      *
*****************************************************************
********************* D G S *************************************

* .SUBCKT STD25N10F7 1 2 3 Tj Tcase
.SUBCKT STD25N10F7    1 2 3

VTj Tj 0 318
VTcase Tcase 0 318

***********************************************************

***********************************************************
*E1    Tj     val_T      VALUE={TEMP}
*R1   val_T   0      1m

.param Vth0=3.5 GFS=0.042 lambda=0.012 Area=0.49 Areacap=0.08

***********************************************************
Rtk Tj 0 1e010
Rtk1 Tj 0 1e010
***********************************************************
VLd 1 d1k 0
V_Read d1k d1 0
VLs 3 ss 0
VLG 2 g2 0

*******************************GRS*********************************
r_grs ss s 1k
V1xx    11xxx ss 0
E1xxx 11xxx s  VALUE={R1(LIMIT(I(V1xx),-1MEG,1MEG),V(TJ))}
.FUNC R1(I1,T1)   {-(I1)*((rs/(Area*(((T1+273)/300)**(unt)/drs))))}

rg1 g2 g {rg}
.PARAM  rg=3.5
******************************************************************
.PARAM   KpSat0=160  KpLin0=160
.PARAM  Rx=1     Rpa=0.2E-06
.PARAM  a=1    B=1        Drs={20m/Area} rs={23m/Area}
.PARAM  Vthx=2.0m KSat=-0.2   KLin=-2.0     Unt=-1.5
.PARAM   resdiodo={3m/Area} RAV=15m
***********************************************************
.FUNC vth1(x) {vth0-vthx*(x-27)}
.FUNC kpsat(x) {kpsat0*((x+273)/300)**(ksat)}
.FUNC kplin(x) {kplin0*((x+273)/300)**(klin)}
.FUNC un(T) {b*((T+273)/300)**(unt)}
*****************************************************************
V_sense  d1   d  0
.PARAM  rd=1u

Gmos d s value {0.9*GFS*(IF(V(d,s)>0,(IF(v(g,s)<vth1(V(TJ)),0,
+(IF((V(d,s)<(v(g,s)-vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(d,s))*kplin(v(TJ))/2*v(d,s)*
+(2*(v(g,s)-vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(d,s)),
+(1+LAMBDA*v(d,s))*kpsat(v(TJ))/2*(v(g,s)-vth1(V(TJ)))**2)))),-(IF(v(g,s)<vth1(V(TJ)),0,
+(IF((V(s,d)<(v(g,s)-vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(s,d))*kplin(v(TJ))/2*v(s,d)*
+(2*(v(g,s)-vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(s,d)),
+(1+LAMBDA*v(s,d))*kpsat(v(TJ))/2*(v(g,s)-vth1(V(TJ)))**2))))))}

.param pir=0.95

Gmos2 d s value {0.1*GFS*(IF(V(d,s)>0,(IF(v(g,s)<pir*vth1(V(TJ)),0,
+(IF((V(d,s)<(v(g,s)-pir*vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(d,s))*kplin(v(TJ))/2*v(d,s)*
+(2*(v(g,s)-pir*vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(d,s)),
+(1+LAMBDA*v(d,s))*kpsat(v(TJ))/2*(v(g,s)-pir*vth1(V(TJ)))**2)))),-(IF(v(g,s)<pir*vth1(V(TJ)),0,
+(IF((V(s,d)<(v(g,s)-pir*vth1(V(TJ)))*kpsat(v(TJ))/kplin(v(TJ))),(1+LAMBDA*v(s,d))*kplin(v(TJ))/2*v(s,d)*
+(2*(v(g,s)-pir*vth1(V(TJ)))-kplin(v(TJ))/kpsat(v(TJ))*v(s,d)),
+(1+LAMBDA*v(s,d))*kpsat(v(TJ))/2*(v(g,s)-pir*vth1(V(TJ)))**2))))))}
R_DS d s 500meg

***********************************************************
****** Capacitance *************
**** CGS *******
CGS 3 g {14.6n*Areacap}
RCGS 3 g 500meg
*******************

***** CMiller******
Edev dev1 0 value {v(d,g)}
c_dev1 dev1 dev2 1E-12
R_dev dev2 dev3 1E-06
V_miller dev3 0 0
G_miller d g  value ={areacap*v(alfa)*i(V_miller)*1E12}
*****************************************************************
Rcap alfa 0 1E03
Ecap alfa 0 TABLE ={V(d,s)}
+(0.1,2691p)
+(0.2,2575p)
+(0.5,2230p)
+(1,1841p)
+(2,1446p)
+(3,1216p)
+(5,950p)
+(7,780p)
+(10,602p)
+(15,411p)
+(25,216p)
+(35,132p)
+(50,63p)
+(60,41p)
+(70,34p)
+(80,31p)
+(90,29p)
+(100,29p)


******************  COSS***********************************

Edevc dev1c 0 value {v(d,s)}
c_dev1c dev1c dev2c 1E-12
R_devc dev2c dev3c 1u
V_millerc dev3c 0 0
G_coss d s  value ={areacap*v(alfa2)*i(V_millerc)*1E12}
*****************************************************************
Rcap2 alfa2 0 1E03
Ecap2 alfa2 0 TABLE ={V(d,s)}
+(0.1,8636p)
+(0.2,8340p)
+(0.5,7573p)
+(1,6789p)
+(2,5964p)
+(3,5472p)
+(5,4889p)
+(7,4511p)
+(10,4103p)
+(15,3613p)
+(25,2965p)
+(35,2529p)
+(50,1920p)
+(60,1506p)
+(70,1261p)
+(80,1137p)
+(90,1054p)
+(100,997p)


****************
V_sense2  sx s  0
***********************************************************
***********************************************************
G_bvdss   d1bvdss1   s  VALUE={I_BVDSS(V(d1bvdss1,s),v(Tj))}
R_GBDSS d1bvdss1 0 1E10
.FUNC  I_BVDSS(z,k1) {(exp(min(-175+z/(bvd(k1)),7))-0p)}
.FUNC bvd(k) {0.69+0.00015*k}
R_bvdss d d1bvdss1 {RAV/Area}

********************* VSD************************************

V_diodo d1zd d1z 0
G_diode s d1zd VALUE={I_diode(V(s,d1zd),v(Tj))}
R_x 3 d1zd 1E7
.FUNC I_diode(z3,k3) {(exp(min(-15+z3/(did(k3)),7))-0)}
.FUNC did(k4) {0.041-70E-06*k4}
R_G_R_didd d d1z {resdiodo}

**************************************************************
**************************************************************
E_E001 ba 0 VALUE {-I(V_diodo)}
*****tr*****
R_R002 aa ba 500
C aa 0 20E-12
*****Irrm****
.param irrm=3.1e4

***nuncanciare****
R_edep d d_dedep 30
E_Eds d_dedep edep VALUE {-V(aa,0)*irrm*Area}
C_Cds edep 3 {1p}



***********************************************************
***************  Thermal Net Junction Case  ***************
***********************************************************
RDUMMY TJ TCASE 1E9
G_Power 0 TJ VALUE ={I(V_read)*v(1,3)}

Rth1 Tj Th1   100m
Rth2 Th1 Th2  157m
Rth3 Th2 Th3  640m
Rth4 Th3 Th4  800m
Rth5 Th4 Tcase 2

Cth1 Tj 0  20u
Cth2 Th1 0  80u
Cth3 Th2 0  130u
Cth4 Th3 0  120u
Cth5 Th4 0  2m
Cth6 Tcase 0  800u

.ENDS STD25N10F7

